apply plugin: 'com.android.application'
def config = parent.ext.android

def isMainModule = parent.ext.global.isMainModule.toBoolean() // 首页
def isKnowModule = parent.ext.global.isKnowModule.toBoolean() // 知识
def isNavModule = parent.ext.global.isNavModule.toBoolean() // 导航
def isMineModule = parent.ext.global.isMineModule.toBoolean() // 我的

android {
    compileSdkVersion config.compileSDKVersion
    buildToolsVersion config.buildToolsVersion
    defaultConfig {
        applicationId "com.felix.mvvm_rxjava_retrofit"
        minSdkVersion config.minSDKVersion
        targetSdkVersion config.targetSDKVersion
        versionCode config.versionCode
        versionName config.versionName
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    dataBinding {
        enabled true
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(':base')

    if (!isMainModule) {
        implementation project(':main')
    }
    if (!isKnowModule) {
        implementation project(':know')
    }
    if (!isNavModule) {
        implementation project(':nav')
    }
    if (!isMineModule) {
        implementation project(':mine')
    }

    // 自动生成网络请求代码
    annotationProcessor project(':apt')
}
